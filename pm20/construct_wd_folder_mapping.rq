# Construct SKOS folder mapping from Wikidata
#
PREFIX pm20: <https://pm20.zbw.eu/folder/>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX pq: <http://www.wikidata.org/prop/qualifier/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wd: <http://www.wikidata.org/entity/>
#
construct {
  ?wd ?skosRelation ?pm20 .
}
where {
  service <https://query.wikidata.org/sparql> {
    # wd items with PM20 ID
    ?wd wdt:P4293 ?pm20Id .
    filter(isLiteral(?pm20Id))
    #
    # mapping relation
    optional {
      ?wd p:P4293/pq:P4390 ?relation .
      # URL of the equivalent skos property
      ?relation wdt:P2699 ?skosRelation1 .
    }
  }
  # bind as URIs
  bind(uri(concat('https://pm20.zbw.eu/folder/', ?pm20Id)) as ?pm20)
  bind(if(bound(?skosRelation1), ?skosRelation1, uri('http://www.w3.org/2004/02/skos/core#exactMatch')) as ?skosRelation)
}
