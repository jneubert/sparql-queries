# Count values of a certain property for PM20 folders
#
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX schema: <http://schema.org/>
PREFIX zbwext: <http://zbw.eu/namespaces/zbw-extensions/>
#
select ?count (str(count(distinct ?pm20)) as ?countLabel) ?value
where {
  values ( ?language ?property ) {
    ( "de" schema:nationality )
  }
  ?pm20 a zbwext:Pm20Folder ;
    ?property ?valueLang .
  filter(lang(?valueLang) = ?language)
  bind(str(?valueLang) as ?value)
  #
  # restrict to folders with online documents
  ?pm20 zbwext:freeDocCount [] .
  #
  bind(uri(concat('http://zbw.eu/beta/sparql-lab/?endpoint=https://zbw.eu/beta/sparql/pm20/query',
        '&queryRef=https://api.github.com/repos/zbw/sparql-queries/contents/pm20/folder_by_value.rq',
        '&language=', ?language, '&property=',
        str(?property), '&value=', ?value)) as ?count)
}
group by ?value ?count
order by desc(count(?value))

