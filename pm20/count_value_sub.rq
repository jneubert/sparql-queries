# Count values of a certain property within a substructure for PM20 folders
#
PREFIX schema: <http://schema.org/>
PREFIX zbwext: <http://zbw.eu/namespaces/zbw-extensions/>
#
select ?count (str(count(distinct ?pm20)) as ?countLabel) ?value
where {
  values ( ?language ?sub ?property ) {
    ( "de" zbwext:activity schema:about )
  }
  ?pm20 a zbwext:Pm20Folder ;
        ?sub [ ?property ?valueLang ].
  filter(lang(?valueLang) = ?language)
  bind(str(?valueLang) as ?value)
  #
  bind(uri(concat('http://zbw.eu/beta/sparql-lab/?endpoint=https://zbw.eu/beta/sparql/pm20/query',
          '&queryRef=https://api.github.com/repos/zbw/sparql-queries/contents/pm20/folder_by_value_sub.rq',
          '&language=', ?language, '&sub=', str(?sub), '&property=', str(?property), '&value="', ?value, '"')) as ?count)
}
group by ?value ?count
order by desc(count(?value))
