# Get all mappings from Wikidata to STW (with labels in German and English)
#
# needs to run from an endpoint holding the vocabulary of the external-id
# in order to get the labels
#
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX pq: <http://www.wikidata.org/prop/qualifier/>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wd: <http://www.wikidata.org/entity/>
#
select distinct ?wd ?wdLabel ?skosRelation ?stw ?stwLabel
where {
  service <https://query.wikidata.org/bigdata/namespace/wdq/sparql> {
    # wd items with STW ID
    ## CAUTION: the following line does not retrive multiple qualified statements
    ## ?wd wdt:P3911 ?stwId .
    ?wd p:P3911 ?statement .
    ?statement ps:P3911 ?stwId .
    filter(isLiteral(?stwId))
    #
    # mapping relation
    optional {
      ?statement pq:P4390 ?relation .
      # URI of the equivalent skos property
      ?relation wdt:P2888 ?skosRelation .
    }
    # get label for the wd item, if exists
    optional {
      ?wd rdfs:label ?wdLabelDe .
      filter(lang(?wdLabelDe) = 'de')
    }
    optional {
      ?wd rdfs:label ?wdLabelEn .
      filter(lang(?wdLabelEn) = 'en')
    }
    #bind(concat(if(bound(?wdLabelEn), str(?wdLabelEn), ''), ' | ', if(bound(?wdLabelDe), str(?wdLabelDe), '')) as ?wdLabel)
    bind(concat(if(bound(?wdLabelDe), str(?wdLabelDe), ''), ' | ', if(bound(?wdLabelEn), str(?wdLabelEn), '')) as ?wdLabel)
  }
  # look up STW descriptors
  bind(uri(concat('http://zbw.eu/stw/descriptor/', ?stwId)) as ?stw)
  #
  # restrict to certain subthes/notation range
  ?stw skos:broader* ?thsys .
  ?thsys skos:notation ?notation .
  ##filter(strstarts(?notation, "P.0"))
  #
  # add labels
  ?stw skos:prefLabel ?stwLabelDe .
  filter(lang(?stwLabelDe) = 'de')
  ?stw skos:prefLabel ?stwLabelEn .
  filter(lang(?stwLabelEn) = 'en')
  bind(concat(if(bound(?stwLabelDe), str(?stwLabelDe), ''), ' | ', if(bound(?stwLabelEn), str(?stwLabelEn), '')) as ?stwLabel)
}
order by ?wdLabel str(?skosRelation) ?stwLabelLang

