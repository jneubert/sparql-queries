# Show all descriptors for a certain category, plus the exactly
# mapped ones in another vocabulary
#
prefix dcterms:  <http://purl.org/dc/terms/>
prefix skos:     <http://www.w3.org/2004/02/skos/core#>
prefix zbwext:   <http://zbw.eu/namespaces/zbw-extensions/>
#
select distinct ?stw (str(?stwLabelLang) as ?stwLabel) ?rel
?foreign (str(?foreignLabelLang) as ?foreignLabel)
where
{
  values ( ?notation ?vocab ?language ) {
    ( "B.01" "thesoz" "en" )
  }
  bind(uri(concat("http://zbw.eu/stw/mapping/", ?vocab, "/target")) as ?target)
  ?category skos:notation ?notation .
  # all subordinated descriptors
  ?category skos:narrower* ?thsys .
  ?thsys a zbwext:Thsys ;
         skos:narrower ?stw .
  ?stw a zbwext:Descriptor ;
       skos:prefLabel ?stwLabelLang .
  filter(lang(?stwLabelLang) = ?language)
  optional {
    ?stw (skos:exactMatch|skos:closeMatch|skos:narrowMatch|skos:broadMatch|skos:relatedMatch) ?foreign .
    ?stw ?rel ?foreign .
    ?foreign dcterms:isPartOf ?target ;
             skos:prefLabel ?foreignLabelLang .
    filter(lang(?foreignLabelLang) = ?language)
  }
}
order by ?stwLabel
