# Get items connected to PM20 film section without pm20Id
#
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX pq: <http://www.wikidata.org/prop/qualifier/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
#
select ?wd ?wdLabel ?subset
where {
  ?wd wdt:P11822 ?section .
  optional {
    { 
      # folder property
      ?wd wdt:P4293 ?folderId .
    } union {
      # qualifier with folder id
      ?wd p:P11822 ?statement .
      ?statement pq:P4293 ?folderId .
    }
  }
  filter(!bound(?folderId))
  
  # get subset (filming/collection)
  bind(substr(?section, 0, 6) as ?subset)  
  
  # label
  ?wd rdfs:label ?label .
  filter(lang(?label) = 'de')
  bind(str(?label) as ?wdLabel)
}

